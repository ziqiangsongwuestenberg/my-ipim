plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.song'
version = '0.0.1-SNAPSHOT'

// OpenTelemetry agent version (single source of truth)
def otelAgentVersion = "2.24.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    otelAgent
}

dependencies {
    // Web & JPA
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //postgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //Flyway
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-database-postgresql"

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform("org.testcontainers:testcontainers-bom:1.20.4")

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    //upload file to s3 bucket
    implementation platform("software.amazon.awssdk:bom:2.25.0")
    implementation "software.amazon.awssdk:s3"

    //prometheus
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // OpenTelemetry Java Agent (download via Gradle, but activated via -javaagent)
    otelAgent "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelAgentVersion}"

    // Logback (for resilience4j)
    implementation "net.logstash.logback:logstash-logback-encoder:7.4"

    // resilience4j (for s3 upload)
    implementation "io.github.resilience4j:resilience4j-spring-boot3:2.2.0"
    implementation "io.github.resilience4j:resilience4j-micrometer:2.2.0"
    implementation "org.springframework.boot:spring-boot-starter-aop"

    // for WebClient
    implementation "org.springframework.boot:spring-boot-starter-webflux"

}

tasks.named('test') {
    useJUnitPlatform()
    // Only force Docker settings when explicitly provided (CI often uses older Docker API).
//    if (System.getenv('DOCKER_HOST') != null) {
//        systemProperty 'docker.host', System.getenv('DOCKER_HOST')
//    }
//    if (System.getenv('DOCKER_API_VERSION') != null) {
//        environment 'DOCKER_API_VERSION', System.getenv('DOCKER_API_VERSION')
//    }
}

// Copy the resolved agent jar into build/otel/opentelemetry-javaagent.jar
tasks.register("copyOtelAgent", Copy) {
    description = "Copies OpenTelemetry Java agent into build/otel/"
    group = "otel"

    from(configurations.otelAgent.singleFile)
    into(layout.buildDirectory.dir("otel"))
    rename { "opentelemetry-javaagent.jar" }
}

// Ensure the agent exists for jar packaging / docker builds
tasks.named("bootJar") {
    dependsOn("copyOtelAgent")
}

// If you run ./gradlew build, make sure the agent is copied as well
tasks.named("build") {
    dependsOn("copyOtelAgent")
}

tasks.named("bootRun") {
    dependsOn("copyOtelAgent")

    doFirst {
        def agentJar = layout.buildDirectory.file("otel/opentelemetry-javaagent.jar").get().asFile
        jvmArgs "-javaagent:${agentJar.absolutePath}",
                "-Dotel.instrumentation.executors.enabled=true"
    }

    environment "OTEL_SERVICE_NAME", "my-ipim"
    environment "OTEL_TRACES_EXPORTER", "otlp"
    environment "OTEL_EXPORTER_OTLP_ENDPOINT", "http://localhost:4317"
    environment "OTEL_EXPORTER_OTLP_PROTOCOL", "grpc"

     // Reduce sampling during development to avoid too many traces.
     environment "OTEL_TRACES_SAMPLER", "parentbased_traceidratio"
     environment "OTEL_TRACES_SAMPLER_ARG", "0.2"
}